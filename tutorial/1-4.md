
##### Step 4: Add Metadata to ERC-721

Import the `Metadata.sol` contract into the header of your ERC-721 token contract, add a new parameter to the contract called `metadata` and set the parameter with the constructor

```solid
pragma solidity ^0.4.20;
import "zeppelin-solidity/contracts/token/ERC721/ERC721Token.sol";
import './Metadata.sol';

/**
 * The Token contract does this and that...
 */
contract Token is ERC721Token {
    Metadata metadata;
    constructor(string name, string symbol, Metadata _metadata) public
        ERC721Token(name, symbol)
    { 
        metadata = _metadata;
    }
}

```

Add a `tokenURI` function that hands the call off to the metadata contract. This will use assembly because you can't return variable length values from external contracts without using `pragma *experimental* ABIEncoderV2` or sing solc 5.0, both of which are considered unstable (and have created problems with verifying the contract on etherscan).

```solid
function getMetadata() public view returns (address) {
    return metadata;
}
function tokenURI(uint _tokenId) public view returns (string _infoUrl) {
    address _impl = getMetadata();
    bytes memory data = msg.data;
    assembly {
        let result := delegatecall(gas, _impl, add(data, 0x20), mload(data), 0, 0)
        let size := returndatasize
        let ptr := mload(0x40)
        returndatacopy(ptr, 0, size)
        switch result
        case 0 { revert(ptr, size) }
        default { return(ptr, size) }
    }
    // this is how it would be done if returning a variable length were possible:
    // return Metadata(metadata).tokenURI(_tokenId);
}
```

Notice we have to use a function that returns the `metadata` address because it's easier to access it as memory that way than reference in the storage inside of the assembly. If we have time at the end of the workshop we can go over this function line by line.

Run `truffle compile` to make sure there are no errors. Your final file should look like this:

```solidity
pragma solidity ^0.4.20;
import "zeppelin-solidity/contracts/token/ERC721/ERC721Token.sol";
import './Metadata.sol';

/**
 * The Token contract does this and that...
 */
contract Token is ERC721Token {
    address metadata;
    constructor(string name, string symbol, address _metadata) public
        ERC721Token(name, symbol)
    { 
        metadata = _metadata;
    }
    function getMetadata() public view returns (address) {
        return metadata;
    }
    function tokenURI(uint _tokenId) public view returns (string _infoUrl) {
        address _impl = getMetadata();
        bytes memory data = msg.data;
        assembly {
            let result := delegatecall(gas, _impl, add(data, 0x20), mload(data), 0, 0)
            let size := returndatasize
            let ptr := mload(0x40)
            returndatacopy(ptr, 0, size)
            switch result
            case 0 { revert(ptr, size) }
            default { return(ptr, size) }
        }
    }
}
```

Commit your changes

```bash
git add . && git commit -m 'Step 4: Add Metadata to ERC-721'
```

[Go to step 5](1-5.md)